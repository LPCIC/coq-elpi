DECLARE PLUGIN "coq-elpi-cs.plugin"

{

open Elpi
open Elpi_plugin
open Coq_elpi_arg_syntax
open Coq_elpi_vernacular
module Evarconv = Evarconv
module Evarconv_hacked = Evarconv_hacked


let elpi_cs_hook program env sigma (t1, sk1) (t2, sk2) =
	  let loc = API.Ast.Loc.initial "(unknown)" in
	  let atts = [] in
	  let (proji, u), arg =
		 match Termops.global_app_of_constr sigma t1 with
		 | (Names.GlobRef.ConstRef proji, u), arg -> (proji, u), arg
		 | _ -> raise Not_found in
	  let structure = Structures.Structure.find_from_projection proji in
	  let params1, c1, extra_args1 =
		 match arg with
		 | Some c -> (* A primitive projection applied to c *)
			let ty = Retyping.get_type_of ~lax:true env sigma c in
			let (i,u), ind_args =
			  (* Are we sure that ty is not an evar? *)
			  Inductiveops.find_mrectype env sigma ty
			in ind_args, c, sk1
		 | None ->
		 match Reductionops.Stack.strip_n_app structure.nparams sk1 with
		 | Some (params1, c1, extra_args1) -> (Option.get @@ Reductionops.Stack.list_of_app_stack params1), c1, extra_args1
       | _ -> raise Not_found in
     let sk2, extra_args2 =
        if List.length sk2 < List.length extra_args1 then raise Not_found
        else match Reductionops.Stack.strip_n_app (List.length sk2 - List.length extra_args1) sk2 with
        | None -> raise Not_found
        | Some (l',el,s') -> ((Option.get @@ Reductionops.Stack.list_of_app_stack l') @ [el],s') in
     let rhs = Reductionops.Stack.zip sigma (t2, Reductionops.Stack.append_app_list sk2 Reductionops.Stack.empty) in
	  let sigma, goal = Evarutil.new_evar env sigma (Retyping.get_type_of env sigma c1) in
	  let goal_evar, _ = EConstr.destEvar sigma goal in
	  let query ~depth state =
		 let state, (loc, q), gls =
			Coq_elpi_HOAS.goals2query sigma [goal_evar] loc ~main:(Coq_elpi_HOAS.Solve [EConstr.mkApp (EConstr.mkConstU (proji, EConstr.EInstance.empty), Array.of_list params1); rhs])
			  ~in_elpi_tac_arg:Coq_elpi_arg_HOAS.in_elpi_tac_econstr ~depth state in
		 let state, qatts = atts2impl loc Summary.Stage.Interp ~depth state atts q in
		 let state = API.State.set Coq_elpi_builtins.tactic_mode state true in
		 state, (loc, qatts), gls
	  in
	  match Interp.get_and_compile program with
	  | None -> None
	  | Some (cprogram, _) ->
		 match Interp.run ~static_check:false cprogram (`Fun query) with
		 | API.Execute.Success solution ->
			let gls = Evar.Set.singleton goal_evar in
			let sigma, _, _ = Coq_elpi_HOAS.solution2evd sigma solution gls in
			if Evd.is_defined sigma goal_evar then
           let lhs = Reductionops.whd_all env sigma (EConstr.mkApp (EConstr.mkConstU (proji, EConstr.EInstance.empty), Array.of_list (params1 @ [goal]))) in
			  let h, sk1 = EConstr.decompose_app sigma lhs in
			  let h2, sk2 = EConstr.decompose_app sigma rhs in
			  let _, params = EConstr.decompose_app sigma (Retyping.get_type_of env sigma goal) in
			  Some (sigma, (h, h2), goal, [], (Array.to_list params, params1), (Array.to_list sk1, Array.to_list sk2), (extra_args1, extra_args2), c1, (None, rhs))
			else None
		 | API.Execute.NoMoreSteps
		 | API.Execute.Failure -> None
    | exception (Coq_elpi_utils.LtacFail (level, msg)) -> None

let add_cs_hook =
  let cs_hook_program = Summary.ref ~name:"elpi-cs" None in
  let cs_hook env sigma proj pat =
    match !cs_hook_program with
    | None -> None
    | Some h -> elpi_cs_hook h env sigma proj pat in
  let name = "elpi-cs" in
  Evarconv_hacked.register_hook ~name cs_hook;
  let inCs =
    let cache program =
      cs_hook_program := Some program;
      Evarconv_hacked.activate_hook ~name in
    let open Libobject in
    declare_object
    @@ superglobal_object_nodischarge "ELPI-CS" ~cache ~subst:None in
  fun program -> Lib.add_leaf (inCs program)

}

VERNAC COMMAND EXTEND ElpiCS CLASSIFIED AS SIDEFF
| #[ atts = any_attribute ] [ "Elpi" "CSFallbackTactic" qualified_name(p) ] -> {
     let () = ignore_unknown_attributes atts in
     add_cs_hook (snd p) }
| #[ atts = any_attribute ] [ "Elpi" "Override" "CS" qualified_name(p) ] -> {
     Evarconv.set_evar_conv Evarconv_hacked.evar_conv_x }

END
