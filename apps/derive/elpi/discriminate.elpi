/* core of discriminate                                                      */
/* license: GNU Lesser General Public License Version 2.1 or later           */
/* ------------------------------------------------------------------------- */

shorten std.{ do! }.

namespace ltac {

% Tests if the command can be applied
func discriminate? term, inductive -> list term, constructor, term, term.
discriminate? Ty GR Args GRA A B :- do! [
  whd Ty [] {{lib:@elpi.eq}} [T,A,B],
  whd T [] (global (indt GR)) Args,
  whd A [] (global (indc GRA)) _,
  whd B [] (global (indc GRB)) _,
  not(GRA = GRB)
].

% Does the job
:functional
:index (1)
pred discriminate! i:term, i:term, o:term, i:inductive, i:list term, i:constructor, i:term, i:term.
discriminate! H G PG GR Args GRA A B :- do! [
  isK-db GRA IsKA,
  coq.mk-app IsKA Args IsKAArgs,
  Eq_isKA_isKB = app[{{lib:elpi.derive.eq_f}},app[global (indt GR)|Args],{{lib:elpi.bool}},IsKAArgs,A,B,H],
  PG = app[{{lib:elpi.bool_discr}},Eq_isKA_isKB,G]
].

func discriminate term, term, term -> term.
discriminate H EqAB G PG :-
  if (discriminate? EqAB    GR Args GRA A B)
     (discriminate! H G PG  GR Args GRA A B)
  (coq.error "discriminate: the equation" {coq.term->string H} "of type" {coq.term->string EqAB} "is trivial at the top level").

}

% vim:set ft=lprolog spelllang=:
